[div=bdocs][ul]
[li][url=https://gist.github.com/gallais/0b3a50e12542f455c010]Self-contained gist[/url][/li]
[/ul][/div]
[pid=chapo]Lately, I have been working quite a bit with
[url=https://github.com/gallais/type-scope-semantics]an abstract notion
of Semantics[/url] defined as a set of combinators all packed in a
record. Doing so, I have come to enjoy the flexibility provided by
parameterised modules to define operations generically over such a
Semantics. In this blog post, I want to show how this pattern can be
used to tidy up some definitions.[/p]

[h3]Introduction: Scope and lambda lifting[/h]

[p]A module declaration in Agda can be parameterised by a telescope of
arguments. These variables are then made available to the user across
the whole body of the module in a manner reminiscent of Coq's
[url=https://coq.inria.fr/distrib/8.4pl6/refman/Reference-Manual004.html#sec77]Sections[/url].
The most bare-bones example I could come up with is an [span=inline-code]Identity[/span]
module parameterised by [span=inline-code]A[/span], a [span=inline-code]Set[/span], and defining the
identity function for [span=inline-code]A[/span]. This definition is accepted without an
out of scope error being raised because, inside the module, the context
is extended with [span=inline-code]A : Set[/span].[/p]

[p=code]module Identity (A : Set) where

  identity : A → A
  identity = λ x → x
[/p]

[p]Once we leave the module, the type of [span=inline-code]identity[/span] as it is
written down does not make sense anymore because [span=inline-code]A[/span]'s scope
was only extending as far as the module was. The process of taking
such a definition and altering it so that it makes sense even outside
of [span=inline-code]Identity[/span] is called
[url=https://en.wikipedia.org/wiki/Lambda_lifting]lambda-lifting[/url].
It consists in prepending the telescope of arguments the module was
parameterised with

[footnote]
This is only a first approximation. It is
naturally possible to have a slightly more subtle approach and to only
prepend the variables which appear free in the type and body of the
defined symbols together will all the ones they themselves depend on
(and so on recursively). In other words, to only use the smallest,
dependency-closed sub-context containing the variables free in the
defined symbol's type and body.
[/footnote]

to the type and body of the various defined symbols it contains. We can
check that [span=inline-code]Identity.identity[/span]'s type now includes an extra argument:
the type [span=inline-code]A[/span]. Outside the [span=inline-code]Identity[/span] module, it is effectively
the polymorphic identity

[footnote]
[p]Here we write [span=inline-code]a ∈ A[/span] to mean that [span=inline-code]a[/span] has type [span=inline-code]A[/span].
This notion can be defined using a simple indexed family which looks a
lot like propositional equality: because of the way the only constructor
[span=inline-code]indeed[/span] is defined, if [span=inline-code]a ∈ A[/span] is inhabited then [span=inline-code]A[/span]
can only ever be [span=inline-code]a[/span]'s type:[/p]

[p=code]data _∈_ {ℓ : Level} {A : Set ℓ} (a : A) : (B : Set ℓ) → Set where
  indeed : a ∈ A
[/p]
[/footnote]

(the highlighted part is the one generated by the lambda-lifting):[/p]

[p=code]check : Identity.identity ∈ [span=highlight]∀ A →[/span] A → A
check = indeed
[/p]


[h3]A more complex example[/h]

[p]In my experience these parameterised modules are particularly helpful
when you need to make the content of a record, passed as an argument,
available to the type and body of a defined symbol. To keep things simple,
I decided to use one of the most basic constructs in the standard library's
[span=inline-code]Algebra[/span]: a [span=inline-code]RawMonoid[/span]. If you have one such beast, then
you can aggregate values by using the monoid's binary operation and
[url=https://www.youtube.com/watch?v=We90tGh1z3g#t=11m29s]people do
that in [i]real[/i] software[/url].[/p]

[p]This module is called [span=inline-code]Reduce[/span], it is parameterised by a [span=inline-code]RawMonoid[/span]

[footnote]
In the standard library, a [span=inline-code]Monoid[/span] is a [span=inline-code]RawMonoid[/span]
plus some laws. Here we do not care about the proofs that [span=inline-code]_∙_[/span]
is associative and [span=inline-code]ε[/span] a neutral element for it. It goes without
saying that in a general-purpose library, it would be more interesting
to work on a [span=inline-code]Monoid[/span] and prove [span=inline-code]aggregate[/span]'s properties
(e.g. that it is a monoid homomorphism).
[/footnote]

and it implements [span=inline-code]aggregate[/span], a function which collapses a list
of values down to a single one. Here it is crucial for us to be able
to bring the fields of the [span=inline-code]RawMonoid mon[/span] in scope for the
whole definition: [span=inline-code]Carrier[/span], [span=inline-code]_∙_[/span] and [span=inline-code]ε[/span] are
all packed up in that record.[/p]

[p=code]module Reduce {c ℓ : Level} (mon : RawMonoid c ℓ) where

  open RawMonoid mon

  aggregate : List Carrier → Carrier
  aggregate = foldr _∙_ ε[/p]

[p]It would of course be possible to write an equivalent function without
using such a module: it all amounts to lambda-lifting the definition by
hand. However no matter the approach we choose (bringing the same constants
in scope or projecting the values out of the record), the definitions
become far less readable:[/p]

[p=code]aggregate′ : {c ℓ : Level} (mon : RawMonoid c ℓ) →
             let open RawMonoid mon in List Carrier → Carrier
aggregate′ mon = let open RawMonoid mon in foldr _∙_ ε

aggregate′′ : {c ℓ : Level} (mon : RawMonoid c ℓ) →
              List (RawMonoid.Carrier mon) → RawMonoid.Carrier mon
aggregate′′ mon = foldr (RawMonoid._∙_ mon) (RawMonoid.ε mon)
[/p]

[p]We can now use [span=inline-code]aggregate[/span] with an extra argument (the
[span=inline-code]RawMonoid[/span] in question). Assuming we have [span=inline-code]ℕ+[/span] and
[span=inline-code]ℕ*[/span] (respectively the additive and multiplicative monoids over
the natural numbers) and a function [span=inline-code][_⋯_][/span] generating a range
of natural numbers, we can write some test about computations involving
[span=inline-code]aggregate[/span]:[/p]

[p=code]test-aggregate : Reduce.aggregate ℕ+ [ 1 ⋯ 5 ] ≡ 15
               ∧ Reduce.aggregate ℕ* [ 1 ⋯ 5 ] ≡ 120
test-aggregate = refl , refl[/p]

[p]One last thing I like to do in this sort of situation, especially
when defining what could be considered a semantics, is to anticipate
the lambda-lifting and define an infix operator which uses a double
turnstile (⊨) to separate the specific theory used from the
(parametrically defined) computation it acts upon. Here this would mean
adding the following line [b]inside[/b] the module [span=inline-code]Reduce[/span]:[/p]

[p=code]  _⊨reduce_ = aggregate[/p]

[p][span=inline-code]_⊨reduce_[/span] is supposed to take two arguments but, inside
[span=inline-code]Reduce[/span], [span=inline-code]aggregate[/span] only takes one. This weird mismatch
is solved when we leave the module and the [span=inline-code]RawMonoid[/span] argument
is lambda-lifted. We can now write [span=inline-code]A ⊨reduce xs[/span] to mean that we use
the monoid [span=inline-code]A[/span] to give a meaning to the phrase "reduce xs".
The same tests as before hold but I find the type more readable:[/p]

[p=code]test-reduce : ℕ+ ⊨reduce [ 1 ⋯ 5 ] ≡ 15
            ∧ ℕ* ⊨reduce [ 1 ⋯ 5 ] ≡ 120
test-reduce = refl , refl[/p]

